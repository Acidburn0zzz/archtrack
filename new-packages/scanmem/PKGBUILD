# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Aaron Griffin <aaron@archlinux.org> 
# Contributor: Elmo Todurov <todurov@gmail.com>
# Contributor: Alexander De Sousa <archaur.xandy21@spamgourmet.com>

pkgname=scanmem
pkgver=0.13
pkgrel=1
pkgdesc="A utility used to locate the address of a variable in an executing process."
arch=('i686' 'x86_64')
url="http://code.google.com/p/scanmem/"
license=('GPL3')
depends=('python2' 'pygtk')
optdepends=('gksu: needed for the GameConqueror GUI.')
source=(http://scanmem.googlecode.com/files/$pkgname-$pkgver.tar.gz)
md5sums=('1c23eda15db242ff0aac96d94be11db1')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	chmod 755 configure
	./configure --enable-gui --prefix=/usr
	msg2 "Building..."
	make -s
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make -s DESTDIR="$pkgdir" install

	# Replacing calls to the Python 3.x interpreter to the Python 2.x one:
	sed 's|python|python2|g' -i "$pkgdir/usr/bin/gameconqueror"
	sed 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' -i \
		"$pkgdir/usr/share/gameconqueror/"{backend.py,GameConqueror.py,hexview.py}
}

# vim: set ft=sh ts=3 sw=3 tw=0:

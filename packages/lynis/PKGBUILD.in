# Archtrack maintainer: Evan Teitelman <teitelmanevan at gmail dot com>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname='lynis'
pkgver=1.3.0
pkgrel=1
groups=(archtrack archtrack-vulnerability-analysis)
pkgdesc='Lynis is an auditing tool for Unix (specialists)'
url='http://www.rootkit.nl/projects/lynis.html'
license=('GPL3')
arch=('i686' 'x86_64')
source=("http://www.rootkit.nl/files/$pkgname-$pkgver.tar.gz")
md5sums=('cb74fbf73b3dc42f838eaedd3ce9f738')

package() {
  cd $pkgname-$pkgver

  # install profile
  install -D -m 644 default.prf "$pkgdir/etc/$pkgname/default.prf"

  # install binary
  install -D -m 750 $pkgname "$pkgdir/usr/sbin/$pkgname"

  # install plugins, include, db
  install -d "$pkgdir/usr/share/$pkgname"
  cp -a db include plugins "$pkgdir/usr/share/$pkgname"
  chmod -R u=rwX,go=rX "$pkgdir/usr/share/$pkgname"

  # install misc files
  install -m 644 -t "$pkgdir/usr/share/$pkgname" README INSTALL CHANGELOG FAQ

  # install manpage
  install -D -m 644 $pkgname.8 "$pkgdir/usr/share/man/man8/$pkgname.8"
}

# vim:set ts=2 sw=2 ft=sh et:

# Archtrack maintainer: Evan Teitelman <teitelmanevan at gmail dot com>
# Contributor: revel <revelΘmuub·net>
# Contributor: shild <sxp@bk.ru>

pkgname='edb-debugger'
pkgver=0.9.18
groups=(archtrack archtrack-reverse-engineering)
pkgrel=2
pkgdesc="EDB (Evan's Debugger) is a QT4 based binary mode debugger with the goal of having usability on par with OllyDbg."
arch=('i686' 'x86_64')
url="http://www.codef00.com/projects.php#Debugger"
license=('GPL2')
depends=('qt4>=4.5')
makedepends=('boost>=1.35.0')
install=edb.install
source=(http://www.codef00.com/projects/debugger-$pkgver.tgz
        edb.desktop)
md5sums=('c34e60c5c5c02c716cdcfb669f74e13f'
         'e9be4ef10362b0ae251dfb8f7d122395')
sha256sums=('dd046187ca6301265f1a2116919a3bf4af64c9ad3fa14809030abbfe82af7b43'
            'a489c5df5018e82ca6899cf850ed19602ed12bfad3b62107fdfde85512355d62')

build() {
  cd $srcdir/debugger

  qmake-qt4 -makefile DEFAULT_PLUGIN_PATH="/usr/lib/edb/"

  # build
  make
}

package() {
  cd $srcdir/debugger

  # install to pkg dir
  make INSTALL_ROOT=$pkgdir/usr install

  # correct /usr/lib64 -> /usr/lib on x86_64
  [ "$CARCH" = "x86_64" ] && (mv $pkgdir/usr/lib64 $pkgdir/usr/lib)

  # maybe post_install message about this script ?
  #install -m755 edb_make_symbolmap.sh $pkgdir/usr/bin

  # icons
  install -D -m 644 src/images/edb48-logo.png $pkgdir/usr/share/pixmaps/edb.png
  install -D -m 644 src/images/edb48-logo.png $pkgdir/usr/share/icons/hicolor/48x48/apps/edb.png

  # install desktop file
  cd ..
  install -D -m 644 edb.desktop $pkgdir/usr/share/applications/edb.desktop
}

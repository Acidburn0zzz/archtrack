# $Id: PKGBUILD 90687 2013-05-13 14:29:09Z spupykin $
# Archtrack maintainer: Evan Teitelman <teitelmanevan at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=miredo-archtrack
_realname=miredo
pkgver=1.2.5
groups=(archtrack archtrack-maintaining-access)
pkgrel=4
pkgdesc='Teredo client and server.'
provides=("miredo=$pkgver")
conflicts=(miredo)
arch=('i686' 'x86_64')
url="http://www.remlab.net/miredo/"
license=('GPL')
depends=('judy' 'iproute2' 'libcap')
backup=('etc/miredo/miredo.conf'
        'etc/miredo/client-hook')
options=('!libtool')
source=(http://www.remlab.net/files/${_realname}/${_realname}-${pkgver}.tar.xz
	miredo.install
	miredo.service)
md5sums=('5114debbf9fcab5d292176e4548f8cd1'
         'd1b655d7a851cdb46c91c3418ed1962f'
         'abd7d8831cab7f3ea6d0d4e51b8f8f6a')

build() {
  cd "$srcdir/$_realname-$pkgver"

  ./configure \
	--prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--libexecdir=/usr/lib \
	--with-Judy
  make
}

package() {
  cd "$srcdir/$_realname-$pkgver"

  make DESTDIR="$pkgdir" install

  sed -i 's#/sbin/ip#/usr/sbin/ip#' $pkgdir/etc/miredo/client-hook

  # avoid conflict with filesystem>=2012.06
  rmdir "$pkgdir/var/run" "$pkgdir/var"

  install -Dm644 "$srcdir/miredo.service" "$pkgdir/usr/lib/systemd/system/miredo.service"
}

# $Id: PKGBUILD 173177 2012-12-12 00:31:59Z eric $
# Archtrack maintainer: Evan Teitelman <teitelmanevan at gmail dot com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

groups=(archtrack archtrack-exploitation-tools archtrack-info-gathering archtrack-maintaining-access archtrack-vulnerability-analysis)
pkgname='nmap'
realname=nmap
pkgver=6.25
provides=("nmap=$pkgver")
conflicts=(nmap)
pkgrel=2
pkgdesc='Utility for network discovery and security auditing'
url='http://nmap.org/'
arch=('i686' 'x86_64')
license=('GPL')
makedepends=('pygtk')
optdepends=('pygtk: zenmap')
depends=('pcre' 'openssl' 'libpcap' 'lua')
source=("http://nmap.org/dist/${realname}-${pkgver}.tar.bz2")
sha1sums=('769943a1a5c10f67bf9738e26da42b3312db752f')

build() {
	cd "${srcdir}/${realname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--mandir=/usr/share/man \

	make
}

package() {
	cd "${srcdir}/${realname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	rm "${pkgdir}"/usr/bin/uninstall_zenmap
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${realname}/LICENSE"
}

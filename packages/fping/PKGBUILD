# $Id: PKGBUILD 166333 2012-09-06 15:45:17Z giovanni $
# Archtrack maintainer: Evan Teitelman <teitelmanevan at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=fping-archtrack
_realname=fping
groups=(archtrack archtrack-info-gathering)
pkgver=3.4
pkgrel=2
provides=("fping=$pkgver")
conflicts=(fping)
pkgdesc='A utility to ping multiple hosts at once'
arch=('i686' 'x86_64')
url="http://www.fping.org/"
license=('custom') 
depends=('glibc')
source=("http://www.fping.org/dist/${_realname}-${pkgver}.tar.gz")
md5sums=('a4bc97804e630dbf26dfac8f28c78091')

build() {
  cd "$srcdir/$_realname-$pkgver"

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-ipv4 \
              --enable-ipv6 \
				  --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/$_realname-$pkgver"

  make DESTDIR="${pkgdir}" install

  # install license
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/$_realname/LICENSE"
  chmod 4555 ${pkgdir}/usr/bin/fping
}

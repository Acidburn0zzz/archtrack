# $Id: PKGBUILD 90182 2013-05-08 09:32:40Z bluewind $
# Archtrack maintainer: Evan Teitelman <teitelmanevan at gmail dot com>
# Contributor: Florian Pritz <flo@xinu.at>
# Contributor: Sven Kauber <celeon@gmail.com>

pkgname=siege-archtrack
_realname=siege
groups=(archtrack archtrack-stress-testing)
pkgver=3.0.0
pkgrel=1
provides=("siege=$pkgver")
conflicts=(siege)
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc='An http regression testing and benchmarking utility'
url="http://www.joedog.org/JoeDog/Siege"
depends=('openssl')
options=(!libtool)
source=("http://www.joedog.org/pub/siege/siege-${pkgver}.tar.gz")
md5sums=('e0ace61f7e01c6b545ac767fc499f817')
backup=(etc/siegerc etc/urls.txt)

build() {
  cd "${srcdir}/$_realname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/$_realname-$pkgver"

  install -d -m755 "$pkgdir/etc"
  install -d -m755 "$pkgdir/usr/share"
  make DESTDIR="$pkgdir" install
  sed -i 's/^# file =$/file = \/etc\/urls.txt/' "$pkgdir/etc/siegerc"
}

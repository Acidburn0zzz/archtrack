# $Id: PKGBUILD 182116 2013-04-07 20:47:05Z guillaume $
# Archtrack maintainer: Evan Teitelman <teitelmanevan at gmail dot com>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Florian Pritz <bluewind at jabber dot ccc dot de>

pkgname=%PKGNAME%
groups=(archtrack archtrack-info-gathering archtrack-sniffing-spoofing)
pkgdesc='a network protocol analyzer - GTK frontend'
pkgver=1.8.6
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('bison' 'flex' 'gtk2' 'krb5' 'libcap' 'libpcap' 'bash' 'gnutls'
             'libgcrypt' 'lua51' 'python')
# Note the archtrack-wireshark-cli dependency. This is an unfortuante
#  consequence of our current package system.
depends=('gtk2' 'wireshark-cli-archtrack' 'desktop-file-utils' 'hicolor-icon-theme')
url='http://www.wireshark.org/'
options=(!libtool)
source=(http://www.wireshark.org/download/src/wireshark-${pkgver}.tar.bz2
        01_patch-automake-1.13.diff
        02_enable-version.diff)
sha256sums=('2722ed3e926c26648faec31a81b7881d829df85762c21794b9dd9e4f227331ea'
            '388a9cf4c924c32a0f7ee8ffeaae060cdbf0c712bcc0032d5dfaa5717b217b80'
            'dd71a9fc3443b5e586d9e50c31208d031f20d7b96cf72d9e4e0f02f2f175d00b')
provides=("wireshark-gtk=$pkgver")
conflicts=(wireshark-gtk)
install=wireshark-gtk.install
replaces=(wireshark)

build() {
  cd "${srcdir}/wireshark-${pkgver}"

# https://bugs.wireshark.org/bugzilla/show_bug.cgi?id=8202
  patch -p0 < ${srcdir}/01_patch-automake-1.13.diff
  patch -p0 < ${srcdir}/02_enable-version.diff

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --with-ssl \
      --with-zlib=yes \
      --with-lua
  make all
}

package() {
  cd "${srcdir}/wireshark-${pkgver}"

  install -Dm755 .libs/wireshark "${pkgdir}/usr/bin/wireshark"
  for d in 16 32 48; do
    install -Dm644 image/hi${d}-app-wireshark.png  \
                   "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png"
  done

  for d in 16 24 32 48 64 128 256 ; do
    install -Dm644 image/WiresharkDoc-${d}.png \
                   "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/mimetypes/application-vnd.tcpdump.pcap.png"
  done
  install -Dm644 wireshark.desktop "${pkgdir}/usr/share/applications/wireshark.desktop"
}
